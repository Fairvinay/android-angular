// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.ajoberstar.grgit.Grgit

apply from: 'config/keystore.gradle'
buildscript {

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.1'
        classpath 'org.jacoco:org.jacoco.core:0.8.7'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
ext.verifyKeystore()

try {
    def grgit = Grgit.open(currentDir: project.rootDir)
    def lastCommit = grgit.head()

    project.ext.versionNum = grgit.log(includes:['HEAD']).size()
    project.ext.versionName = grgit.describe()
    project.ext.versionDate = lastCommit.getDate()
    if (project.ext.versionName == null) {
        project.ext.versionName = 'DEV'
    }
} catch (Exception ignored) {
    project.ext.versionNum = 1
    project.ext.versionName = 'DEV'
    project.ext.versionDate = new Date()
}
project.ext {
    minSdkVersion = 23
    compileSdkVersion = 33

    googleVersions = [
        glide                 : '4.12.0',
        material              : '1.3.0',
    ]

    androidXVersions = [
        annotation            : '1.2.0',
        appCompat             : '1.3.0',
        browser               : '1.3.0',
    ]

    okioVersion = '2.10.0'
    jodaVersion = '2.10.10'
}
task showVersion {
    doLast {
        logger.lifecycle("Version ID: " + project.versionNum)
        logger.lifecycle("Version Name: " + project.versionName)
        logger.lifecycle("Version Date: " + project.versionDate)
    }
}
